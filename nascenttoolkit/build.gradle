buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.uphyca.gradle:gradle-android-aspectj-plugin:0.9.+'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}



plugins {
    id "me.tatarka.retrolambda" version "3.2.5"
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-aspectj'

//region maven config - instructions are here:
// http://code.tutsplus.com/tutorials/creating-and-publishing-an-android-library--cms-24582
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.nascentdigital.nascenttoolkit' // Change this to match your package name
version = '2.1.0' // Change this to match your version number
//endregion



android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}



dependencies {
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'joda-time:joda-time:2.8.1'
}

//Remove linting from build step since lambda expressions break the linter
task lintCheck() {
    getAllTasks(true).each {
        def lintTasks = it.value.findAll { it.name.contains("lint") }
        lintTasks.each {
            it.enabled = false
        }
    }
}

//region more maven config settings
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

bintray {
    //Get these details from https://bintray.com/profile/edit
    user = 'ahinton-nascent'
    key = '213e402596e960cdae2ca9587b1e2988a444abc3'
    pkg {
        repo = 'maven'
        name = 'com.nascentdigital.nascenttoolkit'
        userOrg = 'nascentdigital'
        version {
            name = '2.1.0'
            desc = 'A toolkit to provide common code utils to Android projects.'
            released  = new Date()
            vcsTag = '2.1.0'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nascentdigital/nascent-toolkit-android.git'
        websiteUrl = 'https://github.com/nascentdigital/nascent-toolkit-android'
    }
    configurations = ['archives']
}
//endregion